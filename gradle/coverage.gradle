// Only report code coverage for projects that are distributed
Set<Project> coveredProjects = subprojects.findAll {
    !notPublishableModules.contains(it.path) &&
            it.getExtensions().findByType(JavaPluginExtension) != null
}

task coverage(type: JacocoReport) {
    coveredProjects.each {
        executionData fileTree(it.buildDir).include("/jacoco/*.exec")
        sourceSets it.getExtensions().findByType(JavaPluginExtension).getSourceSets().getByName("main")
    }

    reports {
        xml.enabled true
        html.enabled true
    }

    dependsOn { coveredProjects*.test + coveredProjects*.integrationTest }
}

coveralls {
    sourceDirs = coveredProjects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath = "${buildDir}/reports/jacoco/coverage/coverage.xml"
}

tasks.coveralls {
    group = 'Coverage reports'
    description = 'Uploads the aggregated coverage report to Coveralls'
    dependsOn coverage
    onlyIf { System.env['CI'] }
}
